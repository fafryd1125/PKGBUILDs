# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Robert Hollencamp <rhollencamp@gmail.com>
# Contributor: Daniel J Griffiths <griffithsdj@inbox.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=codeblocks-svn
pkgver=r11005
pkgrel=1
pkgdesc='Cross-platform C/C++ IDE'
arch=('i686' 'x86_64')
url='http://www.codeblocks.org/'
license=('GPL3')
depends=('wxgtk3' 'hunspell' 'gamin')
makedepends=('subversion' 'zip' 'boost')
provides=('codeblocks')
conflicts=('codeblocks')
source=('codeblocks-svn::svn+svn://svn.code.sf.net/p/codeblocks/code/trunk')
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

build() {
  cd "$pkgname"

  ./bootstrap
  ./configure --prefix=/usr --with-contrib-plugins=all
  make
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
}

