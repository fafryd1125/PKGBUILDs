# $Id:
# Maintainer: Darek Zielski <dz1125.bug.tracker@gmail.com>

_pkgname=egl-wayland
pkgname=egl-wayland-git
pkgver=r22.6f5f7d0
pkgrel=1
pkgdesc="The EGLStream-based Wayland external platform"
arch=('x86_64')
url="https://github.com/NVIDIA/egl-wayland"
license=('MIT')
makedepends=('eglexternalplatform' 'wayland' 'mesa' 'git' 'meson')
provides=('egl-wayland')
conflicts=('egl-wayland')
source=("egl-wayland::git+https://github.com/NVIDIA/egl-wayland.git"
        '10_nvidia_wayland.json')
sha256sums=('SKIP'
            '5cccf1905a266e8e34d5ad4aad4be85390e60b1a0850a29dd9d64adc641de412')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir build

#    cd "${_pkgname}"
#    patch -p1 -i ../
}

build() {
    export CC=clang
    export CXX=clang++

    cd build
    meson \
        --prefix=/usr \
        --buildtype=release \
        ../${_pkgname}
    ninja
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install

  cd ../${_pkgname}

  # The NVIDIA EGL driver uses a JSON-based loader to load all EGL External platforms available on the system.
  # If this library is not installed as part of a NVIDIA driver installation, a JSON configuration file  must be
  # manually added in order to make the library work with the NVIDIA driver.
  install -Dm644 "${srcdir}/10_nvidia_wayland.json"  "${pkgdir}/usr/share/egl/egl_external_platform.d/10_nvidia_wayland.json"

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
