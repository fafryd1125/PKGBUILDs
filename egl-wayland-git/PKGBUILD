# $Id:
# Maintainer: Darek Zielski <dz1125.bug.tracker@gmail.com>

_pkgname=egl-wayland
pkgname=egl-wayland-git
pkgver=r1.743d702
pkgrel=1
pkgdesc="The EGLStream-based Wayland external platform"
arch=('i686' 'x86_64')
url="https://github.com/NVIDIA/egl-wayland"
license=('custom:BSD-like')
makedepends=('eglexternalplatform' 'wayland' 'wayland-protocols' 'mesa')
provides=('egl-wayland')
conflicts=('egl-wayland')
source=("egl-wayland::git+https://github.com/NVIDIA/egl-wayland.git"
        'LICENSE'
        '10_nvidia_wayland.json')
sha256sums=('SKIP'
            'ac41d561bbaa48a7c988dd7a5dd47f7ab18d81583d7dc23a1349f0a542bb9313'
            '5cccf1905a266e8e34d5ad4aad4be85390e60b1a0850a29dd9d64adc641de412')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${_pkgname}"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${_pkgname}"
  make DESTDIR="${pkgdir}" install

  # The NVIDIA EGL driver uses a JSON-based loader to load all EGL External platforms available on the system.
  # If this library is not installed as part of a NVIDIA driver installation, a JSON configuration file  must be
  # manually added in order to make the library work with the NVIDIA driver.
  install -Dm644 "${srcdir}/10_nvidia_wayland.json"  "${pkgdir}/usr/share/egl/egl_external_platform.d/10_nvidia_wayland.json"

  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
