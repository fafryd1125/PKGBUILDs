# $Id:
# Maintainer: Darek Zielski <dz1125.bug.tracker@gmail.com>

_pkgname=eglexternalplatform
pkgname=eglexternalplatform-git
pkgver=r2.76e2948
pkgrel=1
pkgdesc="EGL External Platform Interface"
arch=('any')
url="https://github.com/NVIDIA/eglexternalplatform"
license=('MIT')
makedepends=('git')
provides=('eglexternalplatform')
conflicts=('eglexternalplatform')
source=("eglexternalplatform::git+https://github.com/NVIDIA/eglexternalplatform.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "${_pkgname}"

  install -v -Dm644 interface/*.h -t  "${pkgdir}/usr/include/"
  install -v -Dm644 "${_pkgname}.pc" "${pkgdir}/usr/lib/pkgconfig/${_pkgname}.pc"

  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 samples/* -t "${pkgdir}/usr/share/doc/${pkgname}/samples/"

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
