# $Id:
# Maintainer: Darek Zielski <dz1125.bug.tracker@gmail.com>

_pkgname=egl-wayland
pkgname=lib32-egl-wayland-git
pkgver=1.1.3.r2.gbf00742
pkgrel=1
pkgdesc="The EGLStream-based Wayland external platform (32-bit)"
arch=('x86_64')
url="https://github.com/NVIDIA/egl-wayland"
license=('MIT')
depends=('egl-wayland')
makedepends=('eglexternalplatform' 'lib32-wayland' 'mesa' 'lib32-clang' 'git' 'meson')
provides=('lib32-egl-wayland')
conflicts=('lib32-egl-wayland')
source=("egl-wayland::git+https://github.com/NVIDIA/egl-wayland.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir build

#  cd "${_pkgname}"
#  patch -p1 -i ../
}

build() {
  cd build
  meson                               \
      --prefix=/usr                   \
      --libdir=/usr/lib32             \
      --datadir=/usr/lib32            \
      --buildtype=release             \
      --native-file llvm-config       \
      --cross-file x86-linux-gnu-llvm \
      ../${_pkgname}
  ninja

}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install

  cd ../${_pkgname}

  rm -rfv "${pkgdir}/usr/lib32/wayland-eglstream"

  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s "${pkgname#*-}" "${pkgdir}/usr/share/licenses/${pkgname}"
}
