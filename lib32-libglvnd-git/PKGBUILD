# $Id$
# Maintainer: Darek Zielski <dz1125.bug.tracker@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>

# The Meson Build system:     http://mesonbuild.com/index.html
# This is a work-in-progress: https://github.com/aaronp24/libglvnd/tree/meson
# Set "1" to enable

_meson=0

_pkgname=libglvnd
pkgname=lib32-libglvnd-git
pkgver=1.2.0.r0.g062a42e
pkgrel=1
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
source=("libglvnd::git+https://github.com/NVIDIA/libglvnd.git"
        'meson_build.patch')
sha1sums=('SKIP'
          'bee6d5905a6f8bc12ddcc4dabb2949966215a11a')

depends=('lib32-libxext' 'libglvnd-git')
_makedepends=('lib32-libx11' 'git' 'glproto' 'lib32-clang')
provides=('lib32-libglvnd' 'lib32-libgl' 'lib32-libegl' 'lib32-libgles')
conflicts=('lib32-libglvnd')

if [[ ${_meson} = 1 ]]; then
  makedepends=(${_makedepends[@]} 'meson')
else
  makedepends=(${_makedepends[@]} 'python')
fi

pkgver() {
  cd "${_pkgname}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  if [[ ${_meson} = 1 ]]; then
    mkdir build

    cd "${_pkgname}"
    patch -p1 -i ../meson_build.patch # broken
  else
    cd "${_pkgname}"
    # https://github.com/NVIDIA/libglvnd/pulls
#    patch -p1 -i ../
  fi
}

build() {
  if [[ ${_meson} = 1 ]]; then
    cd build
    meson \
        --prefix=/usr \
        --libdir=/usr/lib32/ \
        --buildtype=release \
        --cross-file x86-linux-gnu \
        ../${_pkgname}
    ninja
  else
    export PKG_CONFIG_PATH='/usr/bin/i686-pc-linux-gnu-pkg-config'
#    export CC='clang -m32'
#    export CXX='clang++ -m32'

    cd "${_pkgname}"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib32 \
        --build=i686-pc-linux-gnu \
        --enable-shared \
        --enable-static=no
    make
  fi
}

check() {
  if [[ ${_meson} = 1 ]]; then
    cd build
    ninja test
  else
    cd "${_pkgname}"
    make check
  fi
}

package() {
  if [[ ${_meson} = 1 ]]; then
    cd build
    DESTDIR="${pkgdir}" ninja install
  else
    cd "${_pkgname}"
    make DESTDIR="${pkgdir}" install
  fi

  cd "${srcdir}/${_pkgname}"
  rm -r "${pkgdir}/usr/include"

  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s "${pkgname#*-}" "${pkgdir}/usr/share/licenses/${pkgname}"
}
