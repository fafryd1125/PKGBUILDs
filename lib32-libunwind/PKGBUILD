# $Id$
# Maintainer: Darek Zielski <dz1125.bug.tracker@gmail.com>

_pkgname=libunwind
pkgname=lib32-${_pkgname}
pkgver=1.2
pkgrel=1
pkgdesc='Portable and efficient C programming interface (API) to determine the call-chain of a program (32-bit)'
arch=('x86_64')
url='http://www.nongnu.org/libunwind/'
license=('GPL')
depends=('lib32-glibc' 'libunwind')
makedepends=('gcc-multilib')
source=("http://download.savannah.gnu.org/releases/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('985d0817944e3cafd99aaa5642862d878972e0851f7954289247e808c9319f399ca29342395f0571cb6568a1104a82bd92d585936f2ca888dda37ac796fde0d4'
            'SKIP')
validpgpkeys=('5C96BDEAF5F47FB02BD4F6B965D98560914F3F48'  # Arun Sharma
              '1675C8DA2EF907FB116EB709EC52B396E6874AF2') # Dave Watson

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "${_pkgname}-${pkgver}"
  ./configure \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --build=i686-pc-linux-gnu \
      --disable-documentation
  make
}

check() {
  cd "${_pkgname}-${pkgver}"
  make check || return 0
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  find "${pkgdir}"/usr/include/*.h -not -name "*x86*" -exec rm -f {} \;
}

