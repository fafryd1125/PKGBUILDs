# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libvdpau-git
_pkgname=libvdpau
pkgver=1.2.r2.gc7afb66
pkgrel=1
pkgdesc="Video Decode and Presentation API for UNIX (32-bit)"
arch=('x86_64')
url='https://www.freedesktop.org/wiki/Software/VDPAU/'
license=('MIT')
depends=('lib32-gcc-libs' 'lib32-libxext' 'libvdpau-git')
makedepends=('dri2proto' 'git' 'lib32-clang' 'meson')
provides=('lib32-libvdpau')
conflicts=('lib32-libvdpau')
source=("git+https://gitlab.freedesktop.org/vdpau/libvdpau.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  git describe --long | sed 's/^libvdpau-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  meson ${_pkgname} build \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --buildtype=release \
      --native-file llvm-config \
      --cross-file x86-linux-gnu-llvm
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja install -C build

  cd "${srcdir}/${_pkgname}"

  rm -rf "${pkgdir}"/{etc,usr/include}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s "${pkgname#*-}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
