# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libvdpau-git
_pkgname=libvdpau
pkgver=1.1.1.r3.ga21bf7a
pkgrel=1
pkgdesc="Video Decode and Presentation API for UNIX (32-bit)"
arch=('x86_64')
url='https://www.freedesktop.org/wiki/Software/VDPAU/'
license=('MIT')
depends=('lib32-gcc-libs' 'lib32-libxext' 'libvdpau-git')
makedepends=('dri2proto' 'git' 'lib32-clang' 'meson')
provides=('lib32-libvdpau')
conflicts=('lib32-libvdpau')
source=("git+https://gitlab.freedesktop.org/vdpau/libvdpau.git"
        "${_pkgname}-meson.patch::https://gitlab.freedesktop.org/vdpau/libvdpau/merge_requests/2.patch"
        'x86-linux-gnu')
sha256sums=('SKIP'
            '26ef83e2567db24a0d7ffccd4a76497b15d614cbe57dfdbf5f4f57eb526a515f'
            '02d8f601edb767f787d6bd04bd286fff6339feb0300f3f0d4a79189bb207227f')
#validpgpkeys=('BD68A042C603DDAD9AA354B0F56ACC8F09BA9635') # Aaron Plattner

pkgver() {
  cd "${_pkgname}"
  git describe --long | sed 's/^libvdpau-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir build

  cd "${_pkgname}"
  patch -Np1 -i ../${_pkgname}-meson.patch
}

build() {
  cd build
  meson \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --buildtype=release \
      --cross-file ../x86-linux-gnu \
      ../${_pkgname}
  ninja
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install

  cd "${srcdir}/${_pkgname}"

  rm -rf "${pkgdir}"/{etc,usr/include}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s "${pkgname#*-}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
