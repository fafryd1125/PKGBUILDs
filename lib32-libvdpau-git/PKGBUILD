# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libvdpau-git
_pkgname=libvdpau
pkgver=1.1.1.r3.ga21bf7a
pkgrel=1
pkgdesc="Video Decode and Presentation API for UNIX (32-bit)"
arch=('x86_64')
url='https://www.freedesktop.org/wiki/Software/VDPAU/'
license=('MIT')
depends=('lib32-gcc-libs' 'lib32-libxext' 'libvdpau-git')
makedepends=('dri2proto' 'git' 'lib32-clang')
provides=('lib32-libvdpau')
conflicts=('lib32-libvdpau')
source=("git+https://anongit.freedesktop.org/git/vdpau/libvdpau.git"
        "${_pkgname}-meson.patch::https://github.com/aaronp24/libvdpau/compare/meson.patch"
        'arch-clang32.txt')
sha256sums=('SKIP'
            '08af406f88040e2892c285320637db6748d4e8e8a86a026159ec0e7fce304780'
            'd3d22034971ce44ab5fcd07ed69a4ffe712ff65f35bc8ab8e1132e74eb31fc9a')
#validpgpkeys=('BD68A042C603DDAD9AA354B0F56ACC8F09BA9635') # Aaron Plattner

pkgver() {
  cd "${_pkgname}"
  git describe --long | sed 's/^libvdpau-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir build

  cd "${_pkgname}"
  patch -Np1 -i ../${_pkgname}-meson.patch
}

build() {
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'


  cd build
  meson \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --buildtype=release \
      --cross-file ../arch-clang32.txt \
      ../${_pkgname}
  meson configure
  ninja
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install

  cd "${srcdir}/${_pkgname}"

  rm -rf "${pkgdir}"/{etc,usr/include}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s "${pkgname#*-}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
