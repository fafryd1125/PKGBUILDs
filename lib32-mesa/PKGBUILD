# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=lib32-mesa
_pkgname=mesa
pkgname=('lib32-mesa-vdpau' 'lib32-mesa-glvnd'  'lib32-mesa')
pkgver=17.1.0rc4
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('python2-mako' 'lib32-libxml2' 'lib32-expat' 'lib32-libx11' 'glproto' 'lib32-libdrm' 'dri2proto' 'dri3proto' 'presentproto'
             'lib32-libxshmfence' 'lib32-libxxf86vm' 'lib32-libxdamage' 'gcc-multilib' 'lib32-libelf' 'lib32-llvm' 'lib32-libvdpau'
             'lib32-wayland' 'lib32-libglvnd' 'lib32-libunwind')
url="http://mesa3d.sourceforge.net"
license=('custom')
source=(#https://mesa.freedesktop.org/archive/${pkgver}/mesa-${pkgver}.tar.xz{,.sig}
        https://mesa.freedesktop.org/archive/mesa-17.1.0-rc4.tar.xz{,.sig})
sha256sums=('dadd8f28ca8f5c02ec456bb2919da59b369957d5792f66c2e58016ee8692ea7d'
            'SKIP')
validpgpkeys=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D') # Emil Velikov <emil.l.velikov@gmail.com>
validpgpkeys+=('946D09B5E4C9845E63075FF1D961C596A7203456') #  "Andres Gomez <tanty@igalia.com>"

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config32"

#  cd "${_pkgname}-${pkgver}"
  cd "${_pkgname}-17.1.0-rc4"

  ./configure \
      --build=i686-pc-linux-gnu \
      --host=i686-pc-linux-gnu \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --sysconfdir=/etc \
      --with-dri-driverdir=/usr/lib32/xorg/modules/dri \
      --with-gallium-drivers=nouveau,svga,swrast,virgl \
      --with-dri-drivers=nouveau,swrast \
      --with-egl-platforms=x11,drm,wayland \
      --enable-gallium-llvm \
      --enable-llvm-shared-libs \
      --enable-shared-glapi \
      --enable-libglvnd \
      --disable-gles1 \
      --enable-glx-tls \
      --enable-gbm \
      --enable-dri \
      --enable-dri3 \
      --enable-osmesa \
      --enable-texture-float \
      --enable-vdpau \
      --enable-nine
  make

  # fake installation
  mkdir $srcdir/fakeinstall
  make DESTDIR=${srcdir}/fakeinstall install
}

package_lib32-mesa-vdpau() {
  pkgdesc="Mesa VDPAU drivers (32-bit)"
  depends=('lib32-libdrm' 'lib32-libx11' 'lib32-expat' 'lib32-llvm-libs' 'lib32-libelf' 'lib32-libxshmfence')

  install -m755 -d ${pkgdir}/usr/lib32
  cp -rv ${srcdir}/fakeinstall/usr/lib32/vdpau ${pkgdir}/usr/lib32

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lib32-mesa-glvnd() {
  pkgdesc="Mesa 3-D graphics library (32-bit)"
  depends=('lib32-mesa' 'lib32-libglvnd')

  install -m755 -d ${pkgdir}/usr/lib32/
  #  --enable-libglvnd
  cp -rv ${srcdir}/fakeinstall/usr/lib32/libGLX_mesa.so* ${pkgdir}/usr/lib32/
  cp -rv ${srcdir}/fakeinstall/usr/lib32/libEGL*.so* ${pkgdir}/usr/lib32/

  # indirect rendering
  # uncomment if needed - provided by lib32-nvidia-libgl
#  ln -s /usr/lib32/libGLX_mesa.so.0 ${pkgdir}/usr/lib32/libGLX_indirect.so.0

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lib32-mesa() {
  pkgdesc="an open-source implementation of the OpenGL specification (32-bit)"
  depends=('lib32-libdrm' 'lib32-libxxf86vm' 'lib32-libxdamage' 'lib32-libxshmfence' 'lib32-libelf'
           'lib32-llvm-libs' 'lib32-wayland' 'lib32-libtxc_dxtn' 'mesa')
  optdepends=('opengl-man-pages: for the OpenGL API man pages'
              'lib32-mesa-vdpau: for accelerated video playback')
  provides=('lib32-nouveau-dri' 'lib32-mesa-dri')
  conflicts=('lib32-nouveau-dri' 'lib32-mesa-dri')

  install -m755 -d ${pkgdir}/usr/lib32/xorg/modules/dri
  # nouveau-dri, svga-dri, swrast
  cp -av ${srcdir}/fakeinstall/usr/lib32/xorg/modules/dri/* ${pkgdir}/usr/lib32/xorg/modules/dri

  install -m755 -d ${pkgdir}/usr/lib32
  cp -rv ${srcdir}/fakeinstall/usr/lib32/d3d  ${pkgdir}/usr/lib32

  cp -rv ${srcdir}/fakeinstall/usr/lib32/*.so* ${pkgdir}/usr/lib32/
  # provided by lib32-mesa-glvnd
  rm -fv ${pkgdir}/usr/lib32/{libGLX_mesa.so*,libEGL*.so*,libGLES*.so*}

  cp -rv ${srcdir}/fakeinstall/usr/lib32/pkgconfig ${pkgdir}/usr/lib32/

  # provided by
  # lib32-libglvnd-git + 0001-pkgconfig-add-pkgconfig-data-for-libraries.patch
  rm -fv ${pkgdir}/usr/lib32/pkgconfig/{egl,gl,glesv2}.pc

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}
