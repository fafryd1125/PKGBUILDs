# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=lib32-mesa
_pkgname=mesa
pkgname=('lib32-libva-mesa-driver' 'lib32-mesa-vdpau' 'lib32-mesa-glvnd' 'lib32-mesa')
_rc=rc1
pkgver=18.1.0${_rc}
_pkgver=18.1.0
pkgrel=1
arch=('x86_64')
makedepends=('python2-mako' 'lib32-libxml2' 'lib32-expat' 'lib32-libx11' 'glproto' 'lib32-libdrm' 'dri2proto' 'dri3proto' 'presentproto'
             'lib32-libxshmfence' 'lib32-libxxf86vm' 'lib32-libxdamage' 'gcc-multilib' 'lib32-libelf' 'lib32-llvm' 'lib32-libvdpau'
             'lib32-libglvnd' 'lib32-lm_sensors' 'meson')
url="https://www.mesa3d.org/"
license=('custom')
source=(
#        https://mesa.freedesktop.org/archive/${pkgver}/mesa-${pkgver}.tar.xz{,.sig}
        "https://mesa.freedesktop.org/archive/${_pkgname}-${_pkgver}-${_rc}.tar.xz"
       )
sha256sums=('50d65a58aaff800756796b6aa9eb8fada5f02f2cd7656b40c32b937615b81ef9')
validpgpkeys=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D') # Emil Velikov <emil.l.velikov@gmail.com>
validpgpkeys+=('946D09B5E4C9845E63075FF1D961C596A7203456') # Andres Gomez <tanty@igalia.com>
validpgpkeys+=('E3E8F480C52ADD73B278EE78E1ECBE07D7D70895') # Juan Antonio Su√°rez Romero (Igalia, S.L.) <jasuarez@igalia.com>"

prepare() {
  mkdir build

#  cd "${_pkgname}-${pkgver}"
  cd "${_pkgname}-${_pkgver}-${_rc}"

#  patch -Np1 -i ../
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config32"

  cd build

   local meson_options=(
   -Db_ndebug=true
   -Ddri3=true
   -Ddri-drivers=nouveau
   -Dgallium-drivers=nouveau,swrast
   -Dgallium-nine=true
   -Dgallium-xvmc=false
   -Dvulkan-drivers=''
   -Dvulkan-icd-dir=''
   -Dgles1=false
   -Dglvnd=true
   -Dlibunwind=false
   -Dllvm=true
   -Dosmesa=gallium
   -Dplatforms=x11,drm
   -Dgbm=true
   -Dtexture-float=true
    )

  meson                       \
        --buildtype=release   \
        --sysconfdir=/etc     \
        --prefix=/usr         \
        --libdir=/usr/lib32   \
        "${meson_options[@]}" \
        ../${_pkgname}-${_pkgver}-${_rc}
#        ../${_pkgname}-${pkgver}
#  meson configure
  ninja

  # fake installation
  DESTDIR="${srcdir}/fakeinstall" ninja install
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}

package_lib32-libva-mesa-driver() {
  pkgdesc="VA-API implementation for gallium (32-bit)"
  depends=('lib32-libdrm' 'lib32-libx11' 'lib32-expat' 'lib32-llvm-libs' 'lib32-libelf' 'lib32-libxshmfence')

  _install fakeinstall/usr/lib32/dri/*_drv_video.so

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lib32-mesa-vdpau() {
  pkgdesc="Mesa VDPAU drivers (32-bit)"
  depends=('lib32-libdrm' 'lib32-libx11' 'lib32-expat' 'lib32-llvm-libs' 'lib32-libelf' 'lib32-lm_sensors'
           'lib32-libxshmfence')

  _install fakeinstall/usr/lib32/vdpau

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lib32-mesa-glvnd() {
  pkgdesc="Mesa 3-D graphics library (32-bit)"
  depends=('lib32-mesa' 'lib32-libglvnd')

  #  --enable-libglvnd
  _install fakeinstall/usr/lib32/libGLX_mesa.so*
  _install fakeinstall/usr/lib32/libEGL*.so*

  # indirect rendering
  ln -s /usr/lib32/libGLX_mesa.so.0 ${pkgdir}/usr/lib32/libGLX_indirect.so.0

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lib32-mesa() {
  pkgdesc="an open-source implementation of the OpenGL specification (32-bit)"
  depends=('lib32-libdrm' 'lib32-libxxf86vm' 'lib32-libxdamage' 'lib32-libxshmfence' 'lib32-libelf'
           'lib32-llvm-libs' 'lib32-lm_sensors' 'mesa')
  optdepends=('opengl-man-pages: for the OpenGL API man pages'
              'lib32-mesa-vdpau: for accelerated video playback')
  provides=('lib32-nouveau-dri' 'lib32-mesa-dri')
  conflicts=('lib32-nouveau-dri' 'lib32-mesa-dri')

  # nouveau-dri, swrast
  _install fakeinstall/usr/lib32/dri/*_dri.so

  _install fakeinstall/usr/lib32/d3d

  # provided by lib32-opencl-mesa (lib*OpenCL*) , lib32-mesa-glvnd
  rm -fv fakeinstall/usr/lib32/{lib*OpenCL*,libGLX_mesa.so*,libEGL*.so*,libGLES*.so*}
  _install fakeinstall/usr/lib32/*.so*

  _install fakeinstall/usr/lib32/pkgconfig

  rm -rv fakeinstall/etc
  rm -rv fakeinstall/usr/include
  rm -rv fakeinstall/usr/share

  # make sure there are no files left to install
  find fakeinstall -depth -print0 | xargs -0 rmdir

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgname}" "$pkgdir/usr/share/licenses/${pkgname}"
}
