# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=lib32-mesa
_pkgname=mesa
pkgname=('lib32-vulkan-radeon' 'lib32-libva-mesa-driver' 'lib32-mesa-vdpau' 'lib32-mesa')
pkgver=19.1.4
pkgrel=1
arch=('x86_64')
makedepends=('python-mako' 'lib32-libxml2' 'lib32-expat' 'lib32-libx11' 'glproto' 'lib32-libdrm' 'dri2proto' 'dri3proto' 'presentproto'
             'lib32-libxshmfence' 'lib32-libxxf86vm' 'lib32-libxdamage' 'gcc-multilib' 'lib32-libelf' 'lib32-llvm' 'lib32-libvdpau'
             'lib32-libglvnd' 'lib32-lm_sensors' 'meson' 'lib32-libxrandr')
url="https://www.mesa3d.org/"
license=('custom')
source=(https://mesa.freedesktop.org/archive/${pkgver}/mesa-${pkgver}.tar.xz{,.sig}
       'https://gitlab.freedesktop.org/mesa/mesa/merge_requests/1145.patch')
sha256sums=('a6d268a7d9edcfd92b6da80f2e34e6e0a7baaa442efbeba2fc66c404943c6bfb'
            'SKIP'
            'c35df415fcb960badb2671e24362a99435d52de667e31550889c6cbb1b63f47f')
validpgpkeys=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D') # Emil Velikov <emil.l.velikov@gmail.com>
validpgpkeys+=('946D09B5E4C9845E63075FF1D961C596A7203456') # Andres Gomez <tanty@igalia.com>
validpgpkeys+=('E3E8F480C52ADD73B278EE78E1ECBE07D7D70895') # Juan Antonio Su√°rez Romero (Igalia, S.L.) <jasuarez@igalia.com>"

prepare() {
  mkdir build

  cd "${_pkgname}-${pkgver}"
  patch -Np1 -i ../1145.patch
}

build() {
  cd build

  local meson_options=(
     -Db_ndebug=true
     -Ddri3=true
     -Ddri-drivers=r200,nouveau
     -Dgallium-drivers=radeonsi,nouveau,swrast
     -Dgallium-nine=true
     -Dgallium-xvmc=false
     -Dvulkan-drivers=amd
     -Dgles1=false
     -Dglvnd=true
     -Dglx=dri
     -Dlibunwind=false
     -Dllvm=true
     -Dosmesa=gallium
     -Dplatforms=x11,drm
     -Dgbm=true
  )

  meson                            \
        --buildtype=release        \
        --sysconfdir=/etc          \
        --prefix=/usr              \
        --libdir=/usr/lib32        \
        --cross-file x86-linux-gnu \
        "${meson_options[@]}"      \
        ../${_pkgname}-${pkgver}
#  meson configure
  ninja

  # fake installation
  DESTDIR="${srcdir}/fakeinstall" ninja install
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}

package_lib32-vulkan-radeon() {
  pkgdesc="Radeon's Vulkan mesa driver (32-bit)"
  depends=('lib32-libx11' 'lib32-llvm-libs' 'lib32-libdrm' 'lib32-libelf' 'lib32-libxshmfence')
  provides=('lib32-vulkan-driver')

  _install fakeinstall/usr/share/vulkan/icd.d/radeon_icd*.json
  _install fakeinstall/usr/lib32/libvulkan_radeon.so

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lib32-libva-mesa-driver() {
  pkgdesc="VA-API implementation for gallium (32-bit)"
  depends=('lib32-libdrm' 'lib32-libx11' 'lib32-expat' 'lib32-llvm-libs' 'lib32-libelf' 'lib32-libxshmfence')

  _install fakeinstall/usr/lib32/dri/*_drv_video.so

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lib32-mesa-vdpau() {
  pkgdesc="Mesa VDPAU drivers (32-bit)"
  depends=('lib32-libdrm' 'lib32-libx11' 'lib32-expat' 'lib32-llvm-libs' 'lib32-libelf' 'lib32-lm_sensors'
           'lib32-libxshmfence')

  _install fakeinstall/usr/lib32/vdpau

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lib32-mesa() {
  pkgdesc="an open-source implementation of the OpenGL specification (32-bit)"
  depends=('lib32-libdrm' 'lib32-libxxf86vm' 'lib32-libxdamage' 'lib32-libxshmfence' 'lib32-libelf'
           'lib32-llvm-libs' 'lib32-lm_sensors' 'mesa' 'lib32-libglvnd')
  optdepends=('opengl-man-pages: for the OpenGL API man pages'
              'lib32-mesa-vdpau: for accelerated video playback')
  provides=('lib32-nouveau-dri' 'lib32-mesa-dri' 'lib32-opengl-driver')
  conflicts=('lib32-nouveau-dri' 'lib32-mesa-dri')

  # nouveau-dri, swrast
  _install fakeinstall/usr/lib32/dri/*_dri.so

  _install fakeinstall/usr/lib32/d3d

  # provided by lib32-opencl-mesa (lib*OpenCL*) , lib32-mesa-glvnd
  rm -fv fakeinstall/usr/lib32/{lib*OpenCL*,libGLES*.so*}
  _install fakeinstall/usr/lib32/*.so*

  # indirect rendering
  ln -s /usr/lib32/libGLX_mesa.so.0 ${pkgdir}/usr/lib32/libGLX_indirect.so.0

  _install fakeinstall/usr/lib32/pkgconfig

  rm -rv fakeinstall/usr/include
  rm -rv fakeinstall/usr/share

  # make sure there are no files left to install
  find fakeinstall -depth -print0 | xargs -0 rmdir

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s "${_pkgname}" "$pkgdir/usr/share/licenses/${pkgname}"
}
