# $Id: PKGBUILD 249779 2015-10-27 00:47:25Z eric $
# Maintainer: Darek Zielski <dz1125.bug.tracker@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>

# The Meson Build system:     http://mesonbuild.com/index.html
# This is a work-in-progress: https://github.com/aaronp24/libglvnd/tree/meson
# Set "1" to enable

_meson=1

_pkgname=libglvnd
pkgname=libglvnd-git
pkgver=r582.5baa1e5
pkgrel=1
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
source=("libglvnd::git+https://github.com/NVIDIA/libglvnd.git"
        'LICENSE'
        '0001-khronos-xml-m4.patch'
        'meson_build.patch')
sha1sums=('SKIP'
          'c88e07f333fcf8dcdbef370b85372b4d88ad8a66'
          '88b8f2e4d517ba34a46c4f64ce96840eab28dc4a'
          'bee6d5905a6f8bc12ddcc4dabb2949966215a11a')

depends=('libxext')
_makedepends=('libx11' 'git' 'glproto' 'clang')
provides=('libglvnd' 'libgl' 'libegl' 'libgles')
conflicts=('libglvnd')

if [[ ${_meson} = 1 ]]; then
  makedepends=("${_makedepends[@]}" 'meson')
else
  makedepends=("${_makedepends[@]}" 'python')
fi

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  if [[ ${_meson} = 1 ]]; then
    mkdir build

    cd "${_pkgname}"
    patch -p1 -i ../0001-khronos-xml-m4.patch
    patch -p1 -i ../meson_build.patch
  else
    cd "${_pkgname}"
    # Uncomment if needed

    # *.xml - https://www.khronos.org/
    # *.m4  - https://www.gnu.org/software/autoconf-archive/The-Macros.html#The-Macros
    patch -p1 -i ../0001-khronos-xml-m4.patch

    # https://github.com/NVIDIA/libglvnd/pulls
#    patch -p1 -i ../
  fi
}

build() {
  export 'CC=clang'
  export 'CXX=clang++'

  if [[ ${_meson} = 1 ]]; then
    cd build
    meson \
        --prefix=/usr \
        --buildtype=release \
        ../${_pkgname}
    ninja
  else
    cd "${_pkgname}"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --enable-shared \
        --enable-static=no
    make
  fi
}

package() {
  if [[ ${_meson} = 1 ]]; then
    cd build
    DESTDIR="${pkgdir}" ninja install
  else
    cd "${_pkgname}"
    make DESTDIR="${pkgdir}" install
  fi

  cd "${srcdir}/${_pkgname}"
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
