# $Id:
# Maintainer: Darek Zielski <dz1125.bug.tracker@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_pkgname=libglvnd
pkgname=libglvnd-mesa-git
pkgver=r500.dc16f8c
pkgrel=1
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('i686' 'x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
depends=('libxext')
makedepends=('libx11' 'git' 'glproto' 'python')
provides=('libglvnd' 'libgl')
conflicts=('libglvnd' 'libgl')
source=("libglvnd::git+https://github.com/NVIDIA/libglvnd.git"
        'LICENSE'
        '0001-Update-.xml-files-revision-33335.patch'
        '0001-m4-update-macros.patch'
        '0001-pkgconfig-add-pkgconfig-data-for-libraries.patch'
        'https://patch-diff.githubusercontent.com/raw/NVIDIA/libglvnd/pull/100.patch'
        'https://patch-diff.githubusercontent.com/raw/NVIDIA/libglvnd/pull/107.patch'
        'https://patch-diff.githubusercontent.com/raw/NVIDIA/libglvnd/pull/113.patch')
sha256sums=('SKIP'
            '1a99f7487849c41fc3520e0af3dfcf28a0df05e78956d9ec58c2c52b1f20a77d'
            'c76870613ec9a88a58857011e91db010167594b218a6c8ca9b401d37589fbc90'
            'b0d804f222c588cfb79405db37bf39ba293d2c274e7d86c48bf126d0e586db1f'
            '2cd3ddab728eea8487d84f26cbeb5033db990f41d4ad34f528095443d4b2f7a1'
            'fafdf8f48994c9ba05d818018a1d42cfac861322474fd5dad89b38114acbff14'
            '1b69690925aefe4d6db6817c37c5234b62fdf28d28e37d7d1df37bc550ae5697'
            '149646e01614348dc8e4778932398e83fa79b1f8205145ed73ecef33d6123b24')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_pkgname}"

  # *.xml - https://www.khronos.org/
  # *.m4  - https://www.gnu.org/software/autoconf-archive/The-Macros.html#The-Macros
  patch -p1 -i ../0001-Update-.xml-files-revision-33335.patch
  patch -p1 -i ../0001-m4-update-macros.patch

  # https://github.com/NVIDIA/libglvnd/pull/86 (update)
  patch -p1 -i ../0001-pkgconfig-add-pkgconfig-data-for-libraries.patch

  # https://github.com/NVIDIA/libglvnd/pulls
  patch -p1 -i ../100.patch
  patch -p1 -i ../107.patch
  patch -p1 -i ../113.patch
}

build() {
  cd "${_pkgname}"

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --enable-shared \
      --enable-static=no \
      --disable-gles
  make
}

package() {
  cd "${_pkgname}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
