# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libvdpau-git
_pkgname=libvdpau
pkgver=1.1.1.r3.ga21bf7a
pkgrel=1
pkgdesc="Video Decode and Presentation API for UNIX"
arch=('i686' 'x86_64')
url='https://www.freedesktop.org/wiki/Software/VDPAU/'
license=('MIT')
depends=('gcc-libs' 'libxext')
makedepends=('dri2proto' 'git')
provides=('libvdpau')
conflicts=('libvdpau')
source=("git+https://anongit.freedesktop.org/git/vdpau/libvdpau.git"
        'Merge_remote_branch_from_mosterta.patch')
sha256sums=('SKIP'
            '10e2f8a7c2ec3dcf8063679a85490a73fe77c8a6e2dd4705ff4dbcb93f307e95')
#validpgpkeys=('BD68A042C603DDAD9AA354B0F56ACC8F09BA9635') # Aaron Plattner

pkgver() {
  cd "${_pkgname}"
  git describe --long | sed 's/^libvdpau-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${_pkgname}"

  # https://github.com/mosterta/libvdpau
  patch -Np1 -i ../Merge_remote_branch_from_mosterta.patch
}

build() {
  cd "${_pkgname}"

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${_pkgname}"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
