# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgname=mesa
pkgname=mesa-git
pkgver=18.2.0_devel.102064.b6648798cf
pkgrel=1
pkgdesc="Mesa 3-D graphics library"
arch=('x86_64')
depends=('libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf' 'libomxil-bellagio'
         'libunwind' 'llvm-libs' 'lm_sensors' 'libx11' 'expat' 'libglvnd')
makedepends=('python2-mako' 'libxml2'  'glproto' 'dri2proto' 'dri3proto' 'presentproto'
             'libvdpau' 'libva' 'elfutils' 'llvm' 'libclc' 'clang' 'git' 'meson' 'libxvmc')
provides=('mesa' 'mesa-glvnd' 'mesa-vdpau' 'libva-mesa-driver' 'opengl-driver') # 'vulkan-driver'
conflicts=('mesa' 'mesa-glvnd' 'mesa-vdpau' 'libva-mesa-driver')
url="https://www.mesa3d.org/"
license=('custom')
source=('mesa::git+http://anongit.freedesktop.org/git/mesa/mesa.git#branch=master'
        'LICENSE')
sha256sums=('SKIP'
            '7052ba73bb07ea78873a2431ee4e828f4e72bda7d176d07f770fa48373dec537')
validpgpkeys=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D') # Emil Velikov <emil.l.velikov@gmail.com>

pkgver() {
  cd "${_pkgname}"
  echo $(cat VERSION | tr "-" "_").$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir build
}

build() {
  cd build

   local meson_options=(
   -Db_ndebug=true
   -Ddri3=true
   -Ddri-drivers=nouveau
   -Dgallium-drivers=nouveau,swrast
   -Dgallium-nine=true
   -Dgallium-omx=bellagio
   -Dvulkan-drivers=''
   -Dvulkan-icd-dir=''
   -Dgles1=false
   -Dglvnd=true
   -Dllvm=true
   -Dosmesa=gallium
   -Dplatforms=x11,drm
   -Dgbm=true
   -Dtexture-float=true
    )

  meson                       \
        --buildtype=release   \
        --sysconfdir=/etc     \
        --prefix=/usr         \
        "${meson_options[@]}" \
        ../${_pkgname}
  meson configure
  ninja
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install

  # remove files provided by libglvnd
  rm "$pkgdir"/usr/lib/libGLES*.so*

  # indirect rendering
  ln -s /usr/lib/libGLX_mesa.so.0 ${pkgdir}/usr/lib/libGLX_indirect.so.0

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" "${srcdir}/LICENSE"
}
