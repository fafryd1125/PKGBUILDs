# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=mesa-glvnd-git
_pkgbase=mesa
pkgname=('mesa-glvnd-vdpau-git' 'mesa-glvnd-libgl-git'  'mesa-glvnd-git')
pkgver=17.1.0_devel.88947.956556b3c3
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('python2-mako' 'libxml2' 'libx11' 'glproto' 'libdrm' 'dri2proto' 'dri3proto' 'presentproto'
             'libxshmfence' 'libxxf86vm' 'libxdamage' 'libvdpau' 'libva' 'wayland' 'elfutils' 'llvm'
             'libomxil-bellagio' 'libclc' 'clang' 'libglvnd' 'git')
url="http://mesa3d.sourceforge.net"
license=('custom')
source=('mesa::git+http://anongit.freedesktop.org/git/mesa/mesa.git#branch=master'
        'LICENSE'
        'disable-pthread-stubs-on-linux.patch'
        '0001-glapi-Link-with-glapi-when-built-shared.patch'
        '0002-Revert-gbm-dlopen-libglapi-so-gbm_create_device-work.patch'
        '0003-mesa-glapi-Clean-up-dlopening-glapi-as-we-are-buildi.patch'
        '0001-EGL-Implement-the-libglvnd-interface-for-EGL-v3.patch'
        'Mesa-dev-GLX-Generate-the-libglvnd-dispatch-stub-functions..patch')
sha256sums=('SKIP'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2'
            '09e738ba5059e5871c1452661187ea56c24ff81270f3a958b90c43d9fc1723e2'
            '064dcd5a3ab1b7c23383e2cafbd37859e4c353f8839671d9695c6f7c2ef3260b'
            'c7fac2e4522917d89f0b624e4df7879157fcfc96962a2392a962be70c56692b0'
            'c7eeeb8e176cbc6bdb50a9750660a7047e6273dd437e9765e280457eeca4fcdc'
            'bb88b0b16ddb8361795fcef861c995c8b8f74129ef205fa43775cbb813e25e66'
            'df5c4ecfc6c05f4bff0838bc5193342ee8c28b8fcd9ae10160f0286d1f3bfff4')
validpgpkeys=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D') # Emil Velikov <emil.l.velikov@gmail.com>

pkgver() {
  cd "$_pkgbase"
  echo $(cat VERSION | tr "-" "_").$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$_pkgbase"

  # Now mesa checks for libpthread-stubs - so remove the check
  patch -Np1 -i ../disable-pthread-stubs-on-linux.patch

  # https://bugs.freedesktop.org/show_bug.cgi?id=92877
  # https://patchwork.freedesktop.org/series/12354/
  patch -Np1 -i ../0001-EGL-Implement-the-libglvnd-interface-for-EGL-v3.patch

  # https://bugs.freedesktop.org/show_bug.cgi?id=98428
  # https://github.com/NVIDIA/libglvnd/issues/104
  patch -Np1 -i ../0001-glapi-Link-with-glapi-when-built-shared.patch
  patch -Np1 -i ../0002-Revert-gbm-dlopen-libglapi-so-gbm_create_device-work.patch
  #https://bugs.freedesktop.org/show_bug.cgi?id=98428#c17
#  patch -Np1 -i ../0003-mesa-glapi-Clean-up-dlopening-glapi-as-we-are-buildi.patch
  # https://patchwork.freedesktop.org/series/20113/
  patch -Np1 -i ../Mesa-dev-GLX-Generate-the-libglvnd-dispatch-stub-functions..patch
}

build() {
  cd "$_pkgbase"

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --with-dri-driverdir=/usr/lib/xorg/modules/dri \
      --with-gallium-drivers=nouveau,svga,swrast,virgl \
      --with-dri-drivers=nouveau,swrast \
      --with-egl-platforms=x11,drm,wayland \
      --enable-gallium-llvm \
      --enable-llvm-shared-libs \
      --enable-shared-glapi \
      --enable-libglvnd \
      --disable-gles1 \
      --enable-glx-tls \
      --enable-gbm \
      --enable-dri \
      --enable-dri3 \
      --enable-gallium-osmesa \
      --enable-texture-float \
      --enable-xa \
      --enable-vdpau \
      --enable-omx \
      --enable-nine \
      --with-clang-libdir=/usr/lib
  make

#  --enable-libglvnd       Build GLX and EGL for libglvnd [default=disabled]
#  --enable-libglvnd-glx   Build GLX for libglvnd [default=disabled]
#  --enable-libglvnd-egl   Build EGL for libglvnd [default=disabled]

  # fake installation
  mkdir $srcdir/fakeinstall
  make DESTDIR=${srcdir}/fakeinstall install
}

package_mesa-glvnd-vdpau-git() {
  pkgdesc="Mesa VDPAU drivers (git version)"
  depends=('libdrm' 'libx11' 'llvm-libs' 'expat' 'libelf' 'libxshmfence' "mesa-glvnd-git=${pkgver}")
  provides=('mesa-vdpau')
  conflicts=('mesa-vdpau')

  install -m755 -d ${pkgdir}/usr/lib
  cp -rv ${srcdir}/fakeinstall/usr/lib/vdpau ${pkgdir}/usr/lib

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_mesa-glvnd-libgl-git() {
  pkgdesc="Mesa 3-D graphics library"
  depends=('mesa-glvnd-git' 'libglvnd')
  provides=('mesa-libgl')
  conflicts=('mesa-libgl')

  install -m755 -d ${pkgdir}/usr/lib/
  #  --enable-libglvnd
  cp -rv ${srcdir}/fakeinstall/usr/lib/libGLX_mesa.so* ${pkgdir}/usr/lib/
  cp -rv ${srcdir}/fakeinstall/usr/lib/libEGL*.so* ${pkgdir}/usr/lib/

  install -m755 -d "${pkgdir}/usr/share/glvnd/egl_vendor.d"
  cp -rv ${srcdir}/fakeinstall/usr/share/glvnd/egl_vendor.d/50_mesa.json ${pkgdir}/usr/share/glvnd/egl_vendor.d/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_mesa-glvnd-git() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm' 'wayland' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf'
           'libomxil-bellagio' 'libtxc_dxtn' 'llvm-libs')
  optdepends=('opengl-man-pages: for the OpenGL API man pages'
              'mesa-vdpau: for accelerated video playback'
              'libva-mesa-driver: for accelerated video playback')
  provides=('nouveau-dri' 'svga-dri' 'mesa-dri' 'mesa')
  conflicts=('nouveau-dri' 'svga-dri' 'mesa-dri' 'mesa')

  install -m755 -d ${pkgdir}/etc
  cp -rv ${srcdir}/fakeinstall/etc/drirc ${pkgdir}/etc

  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  # nouveau-dri, svga-dri, swrast
  cp -av ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/* ${pkgdir}/usr/lib/xorg/modules/dri

  cp -rv ${srcdir}/fakeinstall/usr/lib/bellagio  ${pkgdir}/usr/lib
  cp -rv ${srcdir}/fakeinstall/usr/lib/d3d  ${pkgdir}/usr/lib

  cp -rv ${srcdir}/fakeinstall/usr/lib/*.so* ${pkgdir}/usr/lib/
  # provided by opencl-mesa (lib*OpenCL*), mesa-glvnd-libgl-git
  rm -fv ${pkgdir}/usr/lib/{lib*OpenCL*,libGLX_mesa.so*,libEGL*.so*,libGLES*.so*}

  # indirect rendering
  # uncomment if needed - provided by nvidia-libgl
#  ln -s /usr/lib/libGLX_mesa.so.0 ${pkgdir}/usr/lib/libGLX_indirect.so.0

  cp -rv ${srcdir}/fakeinstall/usr/include ${pkgdir}/usr
  cp -rv ${srcdir}/fakeinstall/usr/lib/pkgconfig ${pkgdir}/usr/lib/

  # provided by
  # libglvnd-mesa-git + 0001-pkgconfig-add-pkgconfig-data-for-libraries.patch
  rm -fv ${pkgdir}/usr/lib/pkgconfig/{egl,gl,glesv2}.pc

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
