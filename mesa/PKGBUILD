# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=mesa
pkgname=('opencl-mesa' 'vulkan-radeon' 'libva-mesa-driver'  'mesa-vdpau' 'mesa')
pkgver=19.2.0
pkgrel=1
arch=('x86_64')
makedepends=('python-mako' 'libxml2' 'libx11' 'glproto' 'libdrm' 'dri2proto' 'dri3proto' 'presentproto'
             'libxshmfence' 'libxxf86vm' 'libxdamage' 'libvdpau' 'libva' 'elfutils' 'llvm' 'libomxil-bellagio'
             'libclc' 'clang' 'libunwind'  'libglvnd' 'meson' 'lm_sensors' 'libxvmc' 'libxrandr')
url="https://www.mesa3d.org/"
license=('custom')
source=(https://mesa.freedesktop.org/archive/mesa-${pkgver}.tar.xz{,.sig}
        'LICENSE'
        'https://gitlab.freedesktop.org/mesa/mesa/commit/41b57b8b73ec08a553c691542b00d77180bff422.patch'
        'https://gitlab.freedesktop.org/mesa/mesa/commit/e456a053c3d6ec4f3d4581edcad05c72dfdaa407.patch'
       )
sha256sums=('b060caa2a00f856431160ff7377d0e8f58f2aa48c16ee5a9e265ebdccb10852a'
            'SKIP'
            '7052ba73bb07ea78873a2431ee4e828f4e72bda7d176d07f770fa48373dec537'
            '80202357b49553d8b5fc58231d3ac873d08a7fbd670585b8665b0160dfb4a274'
            'f9a668674ada42bd9b90892df37e8c03a2de2558d779ffa33e75e2097a5755fd')

validpgpkeys=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D') # Emil Velikov <emil.l.velikov@gmail.com>
validpgpkeys+=('946D09B5E4C9845E63075FF1D961C596A7203456') # Andres Gomez <tanty@igalia.com>
validpgpkeys+=('E3E8F480C52ADD73B278EE78E1ECBE07D7D70895') # Juan Antonio Su√°rez Romero (Igalia, S.L.) <jasuarez@igalia.com>"

prepare() {
  mkdir build

  cd "${pkgbase}-${pkgver}"

  patch -Np1 -i ../41b57b8b73ec08a553c691542b00d77180bff422.patch
  patch -Np1 -i ../e456a053c3d6ec4f3d4581edcad05c72dfdaa407.patch
}

build() {
  cd build

  local meson_options=(
     -Db_ndebug=true
     -Ddri3=true
     -Ddri-drivers=r200,nouveau
     -Dgallium-drivers=radeonsi,nouveau,swrast
     -Dgallium-nine=true
     -Dgallium-omx=bellagio
     -Dgallium-opencl=icd
     -Dvulkan-drivers=amd
     -Dgles1=false
     -Dglvnd=true
     -Dglx=dri
     -Dllvm=true
     -Dosmesa=gallium
     -Dplatforms=x11,drm
     -Dgbm=true
  )

  meson                           \
        --buildtype=release       \
        --sysconfdir=/etc         \
        --prefix=/usr             \
        --native-file llvm-config \
        "${meson_options[@]}"     \
        ../"${pkgbase}-${pkgver}"
#  meson configure
  ninja

  # fake installation
  DESTDIR="${srcdir}/fakeinstall" ninja install
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}

package_opencl-mesa() {
  pkgdesc="OpenCL support for AMD/ATI Radeon mesa drivers"
  depends=('expat' 'libdrm' 'libelf' 'libclc' 'clang')
  optdepends=('opencl-headers: headers necessary for OpenCL development')
  provides=('opencl-driver')

  _install fakeinstall/etc/OpenCL
  _install fakeinstall/usr/lib/lib*OpenCL*
  _install fakeinstall/usr/lib/gallium-pipe

  install -m755 -d "${pkgdir}/usr/share/licenses/"
  ln -s "${pkgbase}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_vulkan-radeon() {
  pkgdesc="Radeon's Vulkan mesa driver"
  depends=('libx11' 'libxshmfence' 'libelf' 'libdrm' 'llvm-libs')
  provides=('vulkan-driver')

  _install fakeinstall/usr/share/vulkan/icd.d/radeon_icd*.json
  _install fakeinstall/usr/lib/libvulkan_radeon.so

  install -m755 -d "${pkgdir}/usr/share/licenses/"
  ln -s "${pkgbase}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_libva-mesa-driver() {
  pkgdesc="VA-API implementation for gallium"
  depends=('libdrm' 'libx11' 'llvm-libs' 'expat' 'libelf' 'libxshmfence')

  _install fakeinstall/usr/lib/dri/*_drv_video.so

  install -m755 -d "${pkgdir}/usr/share/licenses/"
  ln -s "${pkgbase}" "${pkgdir}/usr/share/licenses/${pkgname}"
}


package_mesa-vdpau() {
  pkgdesc="Mesa VDPAU drivers"
  depends=('libdrm' 'libx11' 'llvm-libs' 'expat' 'libelf' 'lm_sensors' 'libxshmfence' 'libunwind')

  _install fakeinstall/usr/lib/vdpau

  install -m755 -d "${pkgdir}/usr/share/licenses/"
  ln -s "${pkgbase}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_mesa() {
  pkgdesc="An open-source implementation of the OpenGL specification"
  depends=('libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf'
           'libomxil-bellagio' 'libunwind' 'llvm-libs' 'lm_sensors' 'libglvnd')
  optdepends=('opengl-man-pages: for the OpenGL API man pages'
              'mesa-vdpau: for accelerated video playback'
              'libva-mesa-driver: for accelerated video playback')
  provides=('nouveau-dri' 'svga-dri' 'mesa-dri' 'opengl-driver')
  conflicts=('nouveau-dri' 'svga-dri' 'mesa-dri')

  # nouveau-dri, swrast
  _install fakeinstall/usr/lib/dri/*_dri.so

  _install fakeinstall/usr/lib/bellagio
  _install fakeinstall/usr/lib/d3d

  # provided by opencl-mesa (lib*OpenCL*)
  rm -fv fakeinstall/usr/lib/{lib*OpenCL*,libGLES*.so*}
  _install fakeinstall/usr/lib/*.so*

  _install fakeinstall/usr/share/glvnd/egl_vendor.d/50_mesa.json

  # indirect rendering
  # uncomment if needed - provided by nvidia-drivers
  ln -s /usr/lib/libGLX_mesa.so.0 ${pkgdir}/usr/lib/libGLX_indirect.so.0

  _install fakeinstall/usr/include
  _install fakeinstall/usr/lib/pkgconfig

  _install fakeinstall/usr/share/drirc.d/00-mesa-defaults.conf

  # make sure there are no files left to install
  find fakeinstall -depth -print0 | xargs -0 rmdir

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
