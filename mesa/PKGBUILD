# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=mesa
pkgname=('libva-mesa-driver'  'mesa-vdpau' 'mesa-glvnd' 'mesa')
_rc=rc2
pkgver=18.1.0${_rc}
_pkgver=18.1.0
pkgrel=1
arch=('x86_64')
makedepends=('python2-mako' 'libxml2' 'libx11' 'glproto' 'libdrm' 'dri2proto' 'dri3proto' 'presentproto'
             'libxshmfence' 'libxxf86vm' 'libxdamage' 'libvdpau' 'libva' 'elfutils' 'llvm' 'libomxil-bellagio'
             'libclc' 'clang' 'libunwind'  'libglvnd' 'meson' 'lm_sensors' 'libxvmc')
url="https://www.mesa3d.org/"
license=('custom')
source=(
#        https://mesa.freedesktop.org/archive/mesa-${pkgver}.tar.xz{,.sig}
        "https://mesa.freedesktop.org/archive/${pkgbase}-${_pkgver}-${_rc}.tar.xz"
        'LICENSE'
       )
sha256sums=('39f0a0c748edcd13b1bbefbf203be937a7931f3e325a908c59fecc91d1bd008e'
            '7052ba73bb07ea78873a2431ee4e828f4e72bda7d176d07f770fa48373dec537')
validpgpkeys=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D') # Emil Velikov <emil.l.velikov@gmail.com>
validpgpkeys+=('946D09B5E4C9845E63075FF1D961C596A7203456') # Andres Gomez <tanty@igalia.com>
validpgpkeys+=('E3E8F480C52ADD73B278EE78E1ECBE07D7D70895') # Juan Antonio Su√°rez Romero (Igalia, S.L.) <jasuarez@igalia.com>"

prepare() {
  mkdir build

#  cd "${pkgbase}-${pkgver}"
  cd "${pkgbase}-${_pkgver}-${_rc}"

#  patch -Np1 -i ../
}

build() {
  cd build

   local meson_options=(
   -Db_ndebug=true
   -Ddri3=true
   -Ddri-drivers=nouveau
   -Dgallium-drivers=nouveau,swrast
   -Dgallium-nine=true
   -Dgallium-omx=bellagio
   -Dvulkan-drivers=''
   -Dvulkan-icd-dir=''
   -Dgles1=false
   -Dglvnd=true
   -Dllvm=true
   -Dosmesa=gallium
   -Dplatforms=x11,drm
   -Dgbm=true
   -Dtexture-float=true
    )

  meson                       \
        --buildtype=release   \
        --sysconfdir=/etc     \
        --prefix=/usr         \
        "${meson_options[@]}" \
        ../${pkgbase}-${_pkgver}-${_rc}
#        ../${pkgbase}-${pkgver}
#  meson configure
  ninja

  # fake installation
  DESTDIR="${srcdir}/fakeinstall" ninja install
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}

package_libva-mesa-driver() {
  pkgdesc="VA-API implementation for gallium"
  depends=('libdrm' 'libx11' 'llvm-libs' 'expat' 'libelf' 'libxshmfence')

  _install fakeinstall/usr/lib/dri/*_drv_video.so

  install -m755 -d "${pkgdir}/usr/share/licenses/"
  ln -s "${pkgbase}" "${pkgdir}/usr/share/licenses/${pkgname}"
}


package_mesa-vdpau() {
  pkgdesc="Mesa VDPAU drivers"
  depends=('libdrm' 'libx11' 'llvm-libs' 'expat' 'libelf' 'lm_sensors' 'libxshmfence' 'libunwind')

  _install fakeinstall/usr/lib/vdpau

  install -m755 -d "${pkgdir}/usr/share/licenses/"
  ln -s "${pkgbase}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_mesa-glvnd() {
  pkgdesc="Mesa 3-D graphics library"
  depends=('mesa' 'libglvnd')

  #  --enable-libglvnd
  _install fakeinstall/usr/lib/libGLX_mesa.so*
  _install fakeinstall/usr/lib/libEGL*.so*

  _install fakeinstall/usr/share/glvnd/egl_vendor.d/50_mesa.json

  # indirect rendering
  # uncomment if needed - provided by nvidia-drivers
  ln -s /usr/lib/libGLX_mesa.so.0 ${pkgdir}/usr/lib/libGLX_indirect.so.0

  install -m755 -d "${pkgdir}/usr/share/licenses/"
  ln -s "${pkgbase}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_mesa() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf'
           'libomxil-bellagio' 'libunwind' 'llvm-libs' 'lm_sensors')
  optdepends=('opengl-man-pages: for the OpenGL API man pages'
              'mesa-vdpau: for accelerated video playback'
              'libva-mesa-driver: for accelerated video playback')
  provides=('nouveau-dri' 'svga-dri' 'mesa-dri')
  conflicts=('nouveau-dri' 'svga-dri' 'mesa-dri')

  _install fakeinstall/etc/drirc

  # nouveau-dri, swrast
  _install fakeinstall/usr/lib/dri/*_dri.so

  _install fakeinstall/usr/lib/bellagio
  _install fakeinstall/usr/lib/d3d

  # provided by opencl-mesa (lib*OpenCL*), mesa-glvnd
  rm -fv fakeinstall/usr/lib/{lib*OpenCL*,libGLX_mesa.so*,libEGL*.so*,libGLES*.so*}
  _install fakeinstall/usr/lib/*.so*

  _install fakeinstall/usr/include
  _install fakeinstall/usr/lib/pkgconfig

  # make sure there are no files left to install
  find fakeinstall -depth -print0 | xargs -0 rmdir

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
