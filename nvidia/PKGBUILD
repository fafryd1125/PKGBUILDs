# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia
pkgver=390.42
_extramodules=extramodules-4.15-ARCH
pkgrel=1
pkgdesc='NVIDIA drivers for linux'
arch=('x86_64')
url="http://www.nvidia.com/"
depends=('linux>=4.15' 'linux<4.16')
makedepends=('linux-headers>=4.15' 'linux-headers<4.16')
provides=("nvidia-module=${pkgver}")
conflicts=('nvidia-dkms')
license=('custom')
options=(!strip)
source=("http://us.download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run")
sha256sums=('5f242cf0f7f4dcc45759eee3b4973191114842fe7c146c5ae7a12a2ebea394aa')

_pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"

prepare() {
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
    # patches here
}

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${_pkg}"/kernel
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -Dt "${pkgdir}/usr/lib/modules/${_extramodules}" -m644 \
      "${srcdir}/${_pkg}/kernel"/nvidia{,-drm,-modeset,-uvm}.ko

    find "${pkgdir}" -name '*.ko' -exec gzip -n {} +

    install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf" <<END
blacklist nouveau
options nouveau modeset=0
blacklist nvidiafb
blacklist rivafb
END
}
