# Maintainer: SÃ©bastien Luttringer
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=weston
pkgver=5.0.94
pkgrel=1
pkgdesc='A lightweight and functional Wayland compositor.'
arch=('x86_64')
url='https://wayland.freedesktop.org/'
license=('MIT')
depends=('glibc' 'wayland' 'libxkbcommon' 'libinput' 'pixman' 'libdrm' 'pam'
         'libsystemd' 'cairo' 'libpng' 'libjpeg-turbo' 'libwebp' 'mesa' 'glib2'
         'pango' 'lcms2' 'libx11' 'libxcb' 'dbus' 'libva' 'libxcursor' 'colord')
makedepends=('wayland-protocols' 'git' 'clang' 'lld' 'doxygen' 'meson' 'freerdp')
_commit=cc64cc3717ab39882df37bc244d1ac6cf8526f93  # master
source=("weston::git+https://gitlab.freedesktop.org/wayland/weston.git#commit=$_commit"
        "https://gitlab.freedesktop.org/wayland/weston/merge_requests/129.patch")
sha512sums=('SKIP'
            '677b0dc79e3ce4e563ed880bc17acc2bc06c53834be0122393ea60ef5a4d03943be6ac3447d35522cc46c332a17a8e48dd60db2e6cf67e4e47d88bce1658e988')
#validpgpkeys=('C7223EBE4EF66513B892598911A30156E0E67611' # Bryce Harrington
#              'C0066D7DB8E9AC6844D728715E54498E697F11D7') # Derek Foreman

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd "$pkgname"
    patch -p1 -i ../129.patch
}


build() {

  local meson_options=(
    -Dbackend-drm-screencast-vaapi=false
    -Dscreenshare=false
    -Dbackend-rdp=false
    -Dshell-ivi=false
    -Dsimple-dmabuf-drm=''
  )


    meson "$pkgname" build           \
        --buildtype=release          \
        --prefix=/usr                \
        --libexecdir=/usr/lib/weston \
        --native-file llvm-config    \
        "${meson_options[@]}"
    meson configure build
    ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja install -C build

  cd "$srcdir/$pkgname"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
