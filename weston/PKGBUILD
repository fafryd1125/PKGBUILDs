# Maintainer: SÃ©bastien Luttringer
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=weston
pkgver=6.0.93
pkgrel=1
pkgdesc='A lightweight and functional Wayland compositor.'
arch=('x86_64')
url='https://wayland.freedesktop.org/'
license=('MIT')
depends=('glibc' 'wayland' 'libxkbcommon' 'libinput' 'pixman' 'libdrm' 'pam'
         'libsystemd' 'cairo' 'libpng' 'libjpeg-turbo' 'libwebp' 'mesa' 'glib2'
         'pango' 'lcms2' 'libx11' 'libxcb' 'dbus' 'libva' 'libxcursor' 'colord')
makedepends=('wayland-protocols' 'git' 'clang' 'lld' 'doxygen' 'meson' 'freerdp')
_commit=670ac1c0edbc1a4e60f2fee2fcb1e39b57f853fc  # master
source=("weston::git+https://gitlab.freedesktop.org/wayland/weston.git#commit=$_commit")
sha512sums=('SKIP')
#validpgpkeys=('C7223EBE4EF66513B892598911A30156E0E67611' # Bryce Harrington
#              'C0066D7DB8E9AC6844D728715E54498E697F11D7') # Derek Foreman

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd "$pkgname"
#    patch -p1 -i ../
}


build() {

  local meson_options=(
    -Dbackend-drm-screencast-vaapi=false
    -Dscreenshare=false
    -Dbackend-rdp=false
    -Dshell-ivi=false
    -Dsimple-dmabuf-drm=''
  )


    meson "$pkgname" build           \
        --buildtype=release          \
        --prefix=/usr                \
        --libexecdir=/usr/lib/weston \
        --native-file llvm-config    \
        "${meson_options[@]}"
    meson configure build
    ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja install -C build

  cd "$srcdir/$pkgname"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
