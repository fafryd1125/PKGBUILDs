# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxgtk3-git
_pkgname=wxgtk3
pkgver=3.0.3.r6.gfdf5056dac
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="https://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk3' 'libsm' 'gst-plugins-base-libs')
makedepends=('glu' 'gst-plugins-base' 'git')
optdepends=('webkit2gtk: for webview support')
provides=('wxgtk3' 'wxgtk-common')
conflicts=('wxgtk2' 'wxgtk3' 'wxgtk-common')
source=("wxgtk3::git+https://github.com/wxWidgets/wxWidgets.git#branch=WX_3_0_BRANCH")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${_pkgname}"

  ./autogen.sh
  ./configure               \
      --prefix=/usr         \
      --libdir=/usr/lib     \
      --with-gtk=3          \
      --with-opengl         \
      --enable-graphics_ctx \
      --enable-mediactrl    \
      --enable-webview      \
      --with-regex          \
      --with-libpng         \
      --with-libxpm         \
      --with-libjpeg        \
      --with-libtiff        \
      --disable-precomp-headers
  make
  make -C locale allmo
}

package() {
  cd "${_pkgname}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
