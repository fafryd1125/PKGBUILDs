# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: buddabrod <buddabrod@gmail.com>

pkgname=xf86-video-nouveau-git
_pkgname=xf86-video-nouveau
pkgver=1.0.13.r7.g1516d35
pkgrel=1
pkgdesc="Open Source 2D acceleration driver for nVidia cards"
arch=('i686' 'x86_64')
url="http://nouveau.freedesktop.org/"
license=('GPL')
depends=('libsystemd' 'mesa')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=23' 'systemd' 'git')
provides=('xf86-video-nouveau')
conflicts=('xf86-video-nouveau' 'xorg-server<1.19' 'X-ABI-VIDEODRV_VERSION<23' 'X-ABI-VIDEODRV_VERSION>=24')
install=$pkgname.install
source=('xf86-video-nouveau::git+https://anongit.freedesktop.org/git/nouveau/xf86-video-nouveau.git')
sha256sums=('SKIP')
#validpgpkeys=('B97BD6A80CAC4981091AE547FE558C72A67013C3') # Maarten Lankhorst <maarten@debian.org>

pkgver() {
  cd "$_pkgname"
  git describe --long | sed 's/^xf86-video-nouveau-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgname"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$_pkgname"
  make DESTDIR="$pkgdir" install
}
