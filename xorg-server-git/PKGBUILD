# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=xorg-server-git
pkgname=('xorg-server-git' 'xorg-server-xephyr-git' 'xorg-server-xdmx-git' 'xorg-server-xvfb-git' 'xorg-server-xnest-git'
         'xorg-server-xwayland-git' 'xorg-server-common-git' 'xorg-server-devel-git')
pkgver=1.20.0.r483.g427f8bc00
pkgrel=1
arch=('x86_64')
license=('custom')
groups=('xorg')
url="http://xorg.freedesktop.org"
makedepends=('pixman' 'libx11' 'mesa' 'xtrans' 'libxkbfile' 'libxfont2' 'libpciaccess'
             'libxv' 'libxmu' 'libxrender' 'libxi' 'libxaw' 'libdmx' 'libxtst' 'libxres'
             'xorg-xkbcomp' 'xorg-util-macros' 'xorg-font-util' 'libgcrypt' 'libepoxy'
             'xcb-util' 'xcb-util-image' 'xcb-util-renderutil' 'xcb-util-wm' 'xcb-util-keysyms'
             'libxshmfence' 'libunwind' 'systemd' 'wayland-protocols' 'meson' 'xorgproto'
             'git')
source=("git+https://gitlab.freedesktop.org/xorg/xserver.git"
        'xvfb-run'
        'xvfb-run.1'
        '0001-test-xtest-Initialize-array-with-braces.patch'
        '0001-meson-Fix-Xorg.wrap.patch'
        '0001-meson-fix-sdk_required_modules.patch'
        '0001-replace-MESA_EGL_NO_X11_HEADERS-hack-with-upstream-E.patch'
       )
validpgpkeys=('7B27A3F1A6E18CD9588B4AE8310180050905E40C'
              'C383B778255613DFDB409D91DB221A6900000011'
              'DD38563A8A8224537D1F90E45B8A2D50A0ECD0D3'
              '995ED5C8A6138EB0961F18474C09DD83CAAA50B2')
sha512sums=('SKIP'
            '190fa51c0a69d74e8e24b851de51d1bf191e318e27f1950bfa0f14e403b2e5233d338f76777bf0a767d74496946ea79e94c25b1cedeb3740c011794ddebde6d5'
            'd2ba5be4d656cc1b285346f633315973517a16ce47ea314fe41409477884d08eb2b5e435fbff628ba2bb3a6e35f4d300cdfd6b324f00c09692defbf0264b8de7'
            '80c1d232bfca3304444bcb1a01c3f8aea4062ba929132e8c43864cabc02e5e40f9d61bd8a90e0bc60bfcaa5627d5cf06b6b8c95ff0f8bc9c5dbd15f6e1bb255f'
            '9968f904be95db66c62b5c03cd13c9a94cd7f78c619b07863c8cce042c15ec8e5ddade21e14d1ab224f5b50312ee0f5ac5900c7b9850e127d06671330aad55af'
            'f7afd53b25d104396ae9c751d4b66926ecf04c1c28aba0e7e710b01f677c55a6b2c3dd76ff60bf26e99bc10b9373e6eb6b60ec4f0a89afed04aed881e118fb9d'
            '971cf42b07ff26b3d94e2e5f30ddf4af24727da258e2fa0227a27e14ecb0e4ecc703bda1c8492c247a551731a3918a666cae508bf8cc69188cb755e1ab43fc92')

pkgver() {
  cd xserver
#  git describe --tags | sed 's/^xorg-server-//;s/-/+/g'
  git describe --long | sed 's/^xorg-server-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir build

  cd xserver
  patch -Np1 -i ../0001-meson-Fix-Xorg.wrap.patch
  patch -Np1 -i ../0001-meson-fix-sdk_required_modules.patch
  patch -Np1 -i ../0001-test-xtest-Initialize-array-with-braces.patch
  patch -Np1 -i ../0001-replace-MESA_EGL_NO_X11_HEADERS-hack-with-upstream-E.patch
}

build() {
  cd build

  local meson_options=(
    -Db_lundef=false
    -Ddefault_library=shared
    -Ddmx=true
    -Ddri1=true
    -Ddri2=true
    -Ddri3=true
    -Dglx=true
    -Dipv6=true
    -Dlog_dir=/var/log
    -Dxkb_dir=/usr/share/X11/xkb
    -Dxkb_output_dir=/var/lib/xkb
    -Dxephyr=true
    -Dxnest=true
    -Dxorg=true
    -Dxwayland=true
  )

  meson \
    --prefix=/usr \
    --buildtype=release \
    --native-file llvm-config \
    "${meson_options[@]}" \
    ../xserver
#  meson configure
  ninja

  # fake installation
  DESTDIR="${srcdir}/fakeinstall" ninja install
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}

package_xorg-server-common-git() {
  pkgdesc="Xorg server common files"
  depends=('xkeyboard-config' 'xorg-xkbcomp' 'xorg-setxkbmap')
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" xserver/COPYING

  _install fakeinstall/usr/share/man/man1/Xserver.1*
  _install fakeinstall/usr/lib/xorg/protocol.txt
}

package_xorg-server-git() {
  pkgdesc="Xorg X server"
  depends=('libepoxy' 'libxfont2' 'pixman' 'xorg-server-common-git' 'libunwind' 'dbus' 'libgl' 'xf86-input-libinput'
           'libpciaccess' 'libdrm' 'libxshmfence') # FS#52949
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  # see xorg-server-*/hw/xfree86/common/xf86Module.h for ABI versions - we provide major numbers that drivers can depend on
  # and /usr/lib/pkgconfig/xorg-server.pc in xorg-server-devel pkg
  provides=('X-ABI-VIDEODRV_VERSION=25' 'X-ABI-XINPUT_VERSION=24.1' 'X-ABI-EXTENSION_VERSION=10.0' 'x-server')
  install=xorg-server.install

  # distro specific files must be installed in /usr/share/X11/xorg.conf.d
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"

  _install fakeinstall/usr/bin/{Xorg,cvt,gtf}
  _install fakeinstall/usr/lib/xorg/modules

  _install fakeinstall/usr/share/X11/xorg.conf.d/10-quirks.conf
  _install fakeinstall/usr/share/man/man1/{Xorg*,cvt*,gtf*}
  _install fakeinstall/usr/share/man/{man4,man5}

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-server-xephyr-git() {
  pkgdesc="A nested X server that runs as an X application"
  depends=('libxfont2' 'libgl' 'libepoxy' 'libunwind' 'libsystemd' 'libxv' 'pixman' 'xorg-server-common-git' 'xcb-util-image'
           'xcb-util-renderutil' 'xcb-util-wm' 'xcb-util-keysyms')
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  _install fakeinstall/usr/bin/Xephyr
  _install fakeinstall/usr/share/man/man1/Xephyr*

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-server-xvfb-git() {
  pkgdesc="Virtual framebuffer X server"
  depends=('libxfont2' 'libunwind' 'libsystemd' 'pixman' 'xorg-server-common-git' 'xorg-xauth' 'libgl' 'which')
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  _install fakeinstall/usr/bin/Xvfb
  _install fakeinstall/usr/share/man/man1/Xvfb*

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-server-xnest-git() {
  pkgdesc="A nested X server that runs as an X application"
  depends=('libxfont2' 'libxext' 'libunwind' 'pixman' 'xorg-server-common-git' 'libsystemd')
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  _install fakeinstall/usr/bin/Xnest
  _install fakeinstall/usr/share/man/man1/Xnest*

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-server-xdmx-git() {
  pkgdesc="Distributed Multihead X Server and utilities"
  depends=('libxfont2' 'libxi' 'libxaw' 'libxrender' 'libdmx' 'libxfixes' 'libunwind' 'pixman' 'xorg-server-common-git')
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  _install fakeinstall/usr/bin/{Xdmx,dmx*,vdltodmx,xdmxconfig}
  _install fakeinstall/usr/share/man/man1/{Xdmx*,dmxtodmx*,vdltodmx*,xdmxconfig*}

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-server-xwayland-git() {
  pkgdesc="run X clients under wayland"
  depends=('libxfont2' 'libepoxy' 'libunwind' 'libsystemd' 'libgl' 'pixman' 'xorg-server-common-git')
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  _install fakeinstall/usr/bin/Xwayland

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-server-devel-git() {
  pkgdesc="Development files for the X.Org X server"
  depends=(# see pkgdir/usr/lib/pkgconfig/xorg-server.pc
           'xorgproto' 'pixman' 'mesa' 'libpciaccess'
           # not technically required but almost every Xorg pkg needs it to build
           'xorg-util-macros')
  provides=("${pkgname%-git}")
  conflicts=("${pkgname%-git}")

  _install fakeinstall/usr/include/xorg
  _install fakeinstall/usr/lib/pkgconfig/xorg-server.pc
  _install fakeinstall/usr/share/aclocal/xorg-server.m4

  # make sure there are no files left to install
  find fakeinstall -depth -print0 | xargs -0 rmdir

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
