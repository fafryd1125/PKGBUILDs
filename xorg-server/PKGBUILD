# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=xorg-server
pkgname=('xorg-server' 'xorg-server-xephyr' 'xorg-server-xdmx' 'xorg-server-xvfb' 'xorg-server-xnest'
         'xorg-server-xwayland' 'xorg-server-common' 'xorg-server-devel')
pkgver=1.20.5
pkgrel=1
arch=('x86_64')
license=('custom')
groups=('xorg')
url="http://xorg.freedesktop.org"
makedepends=('pixman' 'libx11' 'mesa' 'xtrans' 'libxkbfile' 'libxfont2' 'libpciaccess'
             'libxv' 'libxmu' 'libxrender' 'libxi' 'libxaw' 'libdmx' 'libxtst' 'libxres'
             'xorg-xkbcomp' 'xorg-util-macros' 'xorg-font-util' 'libgcrypt' 'libepoxy'
             'xcb-util' 'xcb-util-image' 'xcb-util-renderutil' 'xcb-util-wm' 'xcb-util-keysyms'
             'libxshmfence' 'libunwind' 'systemd' 'wayland-protocols' 'meson' 'xorgproto'
             'clang' 'git')
#source=(https://xorg.freedesktop.org/releases/individual/xserver/${pkgbase}-${pkgver}.tar.bz2{,.sig}
source=("git+https://gitlab.freedesktop.org/xorg/xserver.git#branch=server-1.20-branch"
        'xvfb-run'
        'xvfb-run.1'
        '0001-meson-fix-sdk_required_modules.patch'
        "https://gitlab.freedesktop.org/xorg/xserver/merge_requests/184.patch"
        '0001-meson-bump-gl-1.2.patch'
        '0001-meson-Fix-Xorg.wrap.patch'
        '0001-replace-MESA_EGL_NO_X11_HEADERS-hack-with-upstream-E.patch'
       )
validpgpkeys=('7B27A3F1A6E18CD9588B4AE8310180050905E40C'
              'C383B778255613DFDB409D91DB221A6900000011'
              'DD38563A8A8224537D1F90E45B8A2D50A0ECD0D3'
              '995ED5C8A6138EB0961F18474C09DD83CAAA50B2')
sha512sums=('SKIP'
            '190fa51c0a69d74e8e24b851de51d1bf191e318e27f1950bfa0f14e403b2e5233d338f76777bf0a767d74496946ea79e94c25b1cedeb3740c011794ddebde6d5'
            'd2ba5be4d656cc1b285346f633315973517a16ce47ea314fe41409477884d08eb2b5e435fbff628ba2bb3a6e35f4d300cdfd6b324f00c09692defbf0264b8de7'
            'f7afd53b25d104396ae9c751d4b66926ecf04c1c28aba0e7e710b01f677c55a6b2c3dd76ff60bf26e99bc10b9373e6eb6b60ec4f0a89afed04aed881e118fb9d'
            '3d3be34ad9fa976daec53573d3a30a9f1953341ba5ee27099af0141f0ef7994fa5cf84dc08aae848380e6abfc10879f9a67f07601c7a437abf8aef13a3ec9fe1'
            'cbf346dfada7027367dea56dbdb86915a7a1391ad24c8d9834700627f98ba6aee93e0556bcd160c8f72dc402d52aad604bb5b927b3947eb8c2c9db8d7cd99067'
            '25b2d0e97652501e6952da2a10e37b8a6357da6de8e554ff9648d1748f51d9d5f5a43f86fd6e08c2a797a4be61bbd9015f4837fe156a61f42c26d9979a97928e'
            'a8ac756423052c3615aa256547d9a6826a621a428c567a4b8e785a15574964dbc10cbed99333ffee53f2f0cc2d587ac00e16c47e79b949afd8d12952c54685d6')

prepare() {
  mkdir build

  cd xserver
  patch -Np1 -i ../184.patch
  patch -Np1 -i ../0001-meson-fix-sdk_required_modules.patch
  patch -Np1 -i ../0001-meson-bump-gl-1.2.patch
  patch -Np1 -i ../0001-meson-Fix-Xorg.wrap.patch

  # See https://gitlab.freedesktop.org/glvnd/libglvnd/merge_requests/193
  patch -Np1 -i ../0001-replace-MESA_EGL_NO_X11_HEADERS-hack-with-upstream-E.patch
}

build() {
  cd build

  local meson_options=(
    -Ddefault_library=shared
    -Ddmx=true
    -Ddri1=true
    -Ddri2=true
    -Ddri3=true
    -Dglx=true
    -Dipv6=true
    -Dlog_dir=/var/log
#    -Dsuid_wrapper=true
    -Dxkb_dir=/usr/share/X11/xkb
    -Dxkb_output_dir=/var/lib/xkb
    -Dxephyr=true
    -Dxnest=true
    -Dxorg=true
    -Dxwayland=true
  )

  meson \
    --prefix=/usr \
    --buildtype=release \
    --native-file llvm-config \
    "${meson_options[@]}" \
    ../xserver
  meson configure
  ninja

  # fake installation
  DESTDIR="${srcdir}/fakeinstall" ninja install
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}

package_xorg-server-common() {
  pkgdesc="Xorg server common files"
  depends=('xkeyboard-config' 'xorg-xkbcomp' 'xorg-setxkbmap')

##  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" "${pkgbase}-${pkgver}/COPYING"
  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" xserver/COPYING

  _install fakeinstall/usr/share/man/man1/Xserver.1*
  _install fakeinstall/usr/lib/xorg/protocol.txt
}

package_xorg-server() {
  pkgdesc="Xorg X server"
  depends=('libepoxy' 'libxfont2' 'pixman' 'xorg-server-common' 'libunwind' 'dbus' 'libgl' 'xf86-input-libinput'
           'libpciaccess' 'libdrm' 'libxshmfence') # FS#52949

  # see xorg-server-*/hw/xfree86/common/xf86Module.h for ABI versions - we provide major numbers that drivers can depend on
  # and /usr/lib/pkgconfig/xorg-server.pc in xorg-server-devel pkg
  provides=('X-ABI-VIDEODRV_VERSION=24' 'X-ABI-XINPUT_VERSION=24.1' 'X-ABI-EXTENSION_VERSION=10.0' 'x-server')
  install=xorg-server.install

  # distro specific files must be installed in /usr/share/X11/xorg.conf.d
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"

  _install fakeinstall/usr/bin/{Xorg,cvt,gtf}
  _install fakeinstall/usr/lib/xorg/modules
#  _install fakeinstall/usr/libexec #  -Dsuid_wrapper
  _install fakeinstall/usr/share/X11/xorg.conf.d/10-quirks.conf
  _install fakeinstall/usr/share/man/man1/{Xorg*,cvt*,gtf*}
  _install fakeinstall/usr/share/man/{man4,man5}

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-server-xephyr() {
  pkgdesc="A nested X server that runs as an X application"
  depends=('libxfont2' 'libgl' 'libepoxy' 'libunwind' 'libsystemd' 'libxv' 'pixman' 'xorg-server-common' 'xcb-util-image'
           'xcb-util-renderutil' 'xcb-util-wm' 'xcb-util-keysyms')

  _install fakeinstall/usr/bin/Xephyr
  _install fakeinstall/usr/share/man/man1/Xephyr*

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-server-xvfb() {
  pkgdesc="Virtual framebuffer X server"
  depends=('libxfont2' 'libunwind' 'libsystemd' 'pixman' 'xorg-server-common' 'xorg-xauth' 'libgl')

  _install fakeinstall/usr/bin/Xvfb
  _install fakeinstall/usr/share/man/man1/Xvfb*

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-server-xnest() {
  pkgdesc="A nested X server that runs as an X application"
  depends=('libxfont2' 'libxext' 'libunwind' 'pixman' 'xorg-server-common' 'libsystemd')

  _install fakeinstall/usr/bin/Xnest
  _install fakeinstall/usr/share/man/man1/Xnest*

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-server-xdmx() {
  pkgdesc="Distributed Multihead X Server and utilities"
  depends=('libxfont2' 'libxi' 'libxaw' 'libxrender' 'libdmx' 'libxfixes' 'libunwind' 'pixman' 'xorg-server-common')

  _install fakeinstall/usr/bin/{Xdmx,dmx*,vdltodmx,xdmxconfig}
  _install fakeinstall/usr/share/man/man1/{Xdmx*,dmxtodmx*,vdltodmx*,xdmxconfig*}

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-server-xwayland() {
  pkgdesc="run X clients under wayland"
  depends=('libxfont2' 'libepoxy' 'libunwind' 'libsystemd' 'libgl' 'pixman' 'xorg-server-common')

  _install fakeinstall/usr/bin/Xwayland

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-server-devel() {
  pkgdesc="Development files for the X.Org X server"
  depends=(# see pkgdir/usr/lib/pkgconfig/xorg-server.pc
           'xorgproto' 'pixman' 'mesa' 'libpciaccess'
           # not technically required but almost every Xorg pkg needs it to build
           'xorg-util-macros')

  _install fakeinstall/usr/include/xorg
  _install fakeinstall/usr/lib/pkgconfig/xorg-server.pc
  _install fakeinstall/usr/share/aclocal/xorg-server.m4

  # make sure there are no files left to install
  find fakeinstall -depth -print0 | xargs -0 rmdir

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
